1 ON ERROR GOTO 7000
2 KEY(10) ON:ON KEY(10) GOSUB 15000:KEY(9) ON:ON KEY(9) GOSUB 9000'bail outs
9   DIM NJ%(12),SL(64),MP(12),MPE(12),T(12),IDAY%(361),DP%(64),DR(64),C%(14),       M%(24),KR%(24),NTSU%(13),NZD%(13),NTD$(361)
10 KEY OFF:DIM MON(30):DIM P(11):DIM MT(11):SND=100:SENABLE=1:KEY (1) ON:KEY (2)   ON:ON KEY (1) GOSUB 4260:ON KEY (2) GOSUB 4280:REP$="N"
11 DEF SEG:POKE 106,0
12 DEF SEG=0:IF (PEEK(&H410) AND &H30)<>&H30 THEN DEF SEG:GOTO 20
13 WIDTH 80:CLS:PRINT" THIS PROGRAM REQUIRES A COLOR\GRAPHICSï¿½ADAPTER":SYSTEM
20 SCREEN 1:COLOR 0,1:CLS:LINE (0,3)-(8,3),1:LINE (0,4)-(8,4),1:LINE (0,5)-        (8,5),1:GET (0,0)-(8,8),MT:LINE (3,0)-(3,8),1:LINE (4,0)-(4,8),1:LINE (5,0)-    (5,8),1:GET (0,0)-(8,8),P:CLS:WIDTH 80   ' GET plus & minus sign
30 COMMON USED,FD$,FDF$:PRINT:PRINT
40 HH$(1)="N":HH$(2)="S"
45 PRINT:PRINT"NSM - Cornell University":PRINT"This section runs and displays the Newhall simulation":PRINT:PRINT:IF USED>1 GOTO 60
50 PRINT:INPUT "Input data file name";FD$:IF FD$="" THEN GOTO 50
51 IF FD$="?" THEN GOSUB 8000
60 PRINT:PRINT"The water holding capacity of the profile is now set at 200 mm"
61 OPEN "I",#1,FD$
62  INPUT #1,ST$,CO$,RLA1,RLA2,H$,RLO1,RLO2,EW$,EL,ARF,AEV,TRR$,TMA,ST,WT,DIF,      YE(1),YE(2)
63  FOR I=1 TO 12:INPUT #1,MP(I),T(I),MPE(I):NEXT I:CLOSE #1:WHC%=200
64 INPUT"Enter the value of your choice (between 0 and 400):";WHC%:PRINT
70 PRINT"This program can run at a slow(3) or fast(0) speed."
80  INPUT "What's your choice for speed?";DLAY:IF DLAY <>3 AND DLAY <>0              THEN GOTO 80   ELSE PRINT:PRINT:PRINT
81 PRINT:PRINT:PRINT:DLAY=0
82 PRINT:INPUT "Do you want an explanation of the model (Y/N)";REP$:PRINT
83 IF ASC(REP$)>89 THEN REP$=CHR$(ASC(REP$)-32)
84 IF REP$<>"Y" AND REP$<>"N" THEN GOTO 82
90 PRINT"PLEASE WAIT...WORKING NOW TO DETERMINE INITIAL SOIL MOISTURE CONDITIONS"
130 FOR I=1 TO 64:READ DP%(I):NEXT I
140 DATA 8,7,16,6,15,24,5,14,23,32,4,13,22,31,40,3,12,21,30,39,48,2,11,20,29,38,4   7,56,1,10,19,28,37,46,55,64,9,18
150 DATA 27,36,45,54,63,17,26,35,44,53,62,25,34,43,52,61,33,42,51,60,41,50,59,49,   58,57
160 FOR I=1 TO 64:READ DR(I):NEXT I
170 DATA 1.,1.,1.,1.,1.02,1.03,1.05,1.07,1.09,1.11,1.13,1.15,1.17,1.19,1.21,1.23,   1.26,1.28,1.31,1.34,1.37,1.40,1.43,1.46
180 DATA 1.49,1.53,1.57,1.61,1.65,1.69,1.74,1.78,1.84,1.89,1.95,2.01,2.07,2.14,2.   22,2.30,2.38,2.47,2.57,2.68,2.80,2.93
190 DATA 3.07,3.22,3.39,3.58,3.80,4.03,4.31,4.62,4.98,5.,5.,5.,5.,5.,5.,5.,5.,5.
200 IF REP$<>"Y" THEN GOTO 240
210 SCREEN 1:COLOR 12,1:LOCATE 2,9:PRINT"THE SOIL MOISTURE PROFILE"
220 VIEW(104,40)-(232,168),,3
230 WINDOW(0,0)-(8,8)
240 '
250 FSL=WHC%/64
270 ANS$=" ":FOR N=1 TO 64:SL(N)=0!:NEXT N:K%=1:SWST%=0:SWFI%=0
280 FOR N=1 TO 3:NTWI%(N)=0:NTSU%(N)=0:NSD%(N)=0:NZD%(N)=0:ND%(N)=0:CC%(N)=0:       NEXT N
290 FOR I=1 TO 5:CD%(I)=0:NEXT I
300 IF REP$<>"Y" THEN GOTO 330
310 LOCATE 4,6:PRINT"EIGHT LAYERS"
320 L$="12345678":FOR I=1 TO LEN(L$):LOCATE 5+I*2,12:PRINT MID$(L$,I,1):NEXT I
330 MSW%= -1:ICON%=0:LT5C%=0:LT8C%=0:IE%=0:IB%=1:PRMO=0!:NCCD%=0:NCCM%=0:           ID8C%=0:ID5C%=0:SWT%=0:TC%=0
340 NP%=0:NP8%=0:NCSM%=0:NCWM%=0:NCSP%=0:NCWP%=0
350 IF REP$="Y" THEN LOCATE 4,26:PRINT"CONTAINING":FOR I=1 TO 8:LOCATE 5+I*2,32:    PRINT"25 MM":NEXT I
360 FOR I=1 TO 6:NBD%(I)=0:NED%(I)=0:NBD8%(I)=0:NED8%(I)=0:NEXT I
370 IF REP$<>"Y" THEN GOTO 400
380 FOR I=7 TO 1 STEP -1
390 LINE(0,I)-(8,I),3:NEXT I
400 FOR I=1 TO 360:IDAY%(I)=0:NEXT I:FC=2.5:FCD=.66:SWP%=-1:GOGR%=0
410 IF REP$="Y" THEN LOCATE 23,6:PRINT"BETWEEN PWP....AND.....FC"
420 FOR JP=1 TO 12
430 IF MPE(JP)>MP(JP) GOTO 460
440 NEXT JP
450 CD%(5)=-1:SWT%=-1 'TO INDICATE PERUDIC REGIME
460 IF REP$="Y" THEN FOR I=1 TO 7:LINE(I,8)-(I,0),3:FOR K=1 TO 300:NEXT K:NEXT
470 FOR N=1 TO 10:IF N=1 AND REP$="Y" THEN GOTO 4080
480 FOR IM=1 TO 12 
485 IF REP$<>"Y" THEN PRINT".";
490 GOSUB 2750:GOSUB 2070
500 GOSUB 2750:NEXT IM:IF N=1 AND REP$="Y" THEN GOTO 4100
510 TMOI=0!:FOR IT=1 TO 64:TMOI=TMOI+SL(IT):NEXT IT
520 IF ABS(TMOI-PRMO)<=(PRMO/100!) THEN GOTO 541  'EXIT FROM PRELIMINARY RUNS
530 PRMO=TMOI
540 NEXT N
541 IF REP$="Y" THEN GOSUB 4170:LOCATE 25,1:INPUT"Press ENTER to continue",X$
550 GOGR%=-1:GOSUB 2880:GOSUB 3760
560 READ A$:IF A$<>"AOXOLOTL" THEN PRINT"DATA POINTER WRONG IN 255":STOP:ELSE A$    =""
570 FOR IM=1 TO 12:LOCATE 25,1:PRINT "Press F1 to pause        ";
580 READ A$:LOCATE 14,4:PRINT "          ":LOCATE 14,4:PRINT A$:LOCATE 15,4:        PRINT"First Half ":FOR DELAY = 1 TO 15*DLAY:NEXT DELAY
590 GOSUB 1630
600 LOCATE 16,4:PRINT "STORM    ":LOCATE 15,4:PRINT"           ":FOR DELAY =        1 TO 3000:NEXT DELAY:GOSUB 2070:LOCATE 16,4:PRINT"        "
610 LOCATE 15,4:PRINT "Second Half":FOR DELAY = 1 TO 15*DLAY:NEXT DELAY:            GOSUB 2880:GOSUB 1630:NEXT IM
620 DATA AOXOLOTL,January, February, March, April, May, June, July, August,         September, October, November, December
630 SN%=0:FOR I=1 TO 12'START EXAMINATION OF CALENDER
640 IF T(I)<5! GOTO 670'TO DETERMINE SEASON > 5 d C
650 NEXT I
660 GOTO 870 'WHEN NO TEMP.BELOW 5 DEG.C
670 T13=T(1):FOR IT=1 TO 11
680 IF T(IT)=5! AND T(IT+1)=5! THEN T(IT)=5.01
690 NEXT IT
700 IF T(12)=5! AND T13=5! THEN T(12)=5.01
710 CRR!=5!
720 GOSUB 2410 'TO DEFINE BEGINNING AND END OF SEASON
730 FOR I=1 TO 6:NBD%(I)=NBJ%(I):NED%(I)=NEJ%(I):NEXT I
740 NP%=NPJ%
750 TC%=-1:IF NP%=0 GOTO 840' WHEN NO TEMP.> 5 DEG C.
760 FOR I=1 TO NP%:IB%=NBD%(I):IF NBD%(I)<NED%(I) GOTO 790
770 IR%=361-NBD%(I)+NED%(I)
780 JB%=IB%:JR%=IR%:GOTO 800' OR WH04F7
790 IR%=NED%(I)-NBD%(I)+1:JB%=IB%:JR%=IR%
800 GOSUB 2380' TO CALCULATE NSD
810 FOR IC=1 TO 3:NSD%(IC)=NSD%(IC)+NZD%(IC):NEXT IC
820 LT5C%=LT5C%+IR%:NEXT I
830 ID5C%=NBD%(1):GOTO 890' TO DETERMINE SEASON > 8 DEG C
840 IF WT>5! GOTO 870
850 LT5C%=0:NBD%(1)=-1:ID5C%=0
860 FOR IK=1 TO 3:NSD%(IK)=0:NEXT IK:TC%=0:GOTO 890
870 TC%=0:FOR IC=1 TO 3:NSD%(IC)=ND%(IC):NEXT IC
880 LT5C%=360:ID5C%=0
890 FOR IC=1 TO 2:NCPM%(IC)=0:NEXT IC:FOR IC=1 TO 12
900 IF T(IC)<8! GOTO 930'DETERMINE SEASON > 8 DEG C
910 NEXT IC
920 GOTO 1170
930 T13=T(1):FOR IT=1 TO 11'PROCESS SEASONS > 8 DEG C.
940 IF T(IT)=8! AND T(IT+1)=8! THEN T(IT)=8.01
950 NEXT IT
960 CRR!=8!
970 IF (T(12)=8! AND T13=8!) THEN T(12)=8.01
980 GOSUB 2410'OR WH04T1
990 FOR I=1 TO 6:NBD8%(I)=NBJ%(I)
1000 NED8%(I)=NEJ%(I):NEXT I
1010 NP8%=NPJ%
1020 TC%= -1:IF NP8%=0 GOTO 1150'WHEN NO TEMP > 8 DEG C.
1030 FOR I = 1 TO NP8%
1040 IB%=NBD8%(I)
1050 IF NBD8%(I)<NED8%(I) GOTO 1070
1060 IR%=361-NBD8%(I)+NED8%(I):GOTO 1080
1070 IR%=NED8%(I)-NBD8%(I)+1
1080 MSW%=0
1090 SIB%=IB%:SIR%=IR%:X%=1:SWM%=MSW%:GOSUB 2160'OR WH04F5 FOR NCPM(2)
1100 ICON%=MAX%:IF NCPM%(2)>ICON% GOTO 1130
1110 NCPM%(2)=ICON%:LT8C%=IR%
1120 ID8C%=NBD8%(I)
1130 NEXT I
1140 SN%=-1:GOTO 1180
1150 IF WT>8! GOTO 1170
1160 LT8C%=0:NBD8%(1)=-1:ID8C%=0:NCPM%(2)=0:MSW%=0 :GOTO 1140
1170 MSW%=0:TC%=0:LT8C%=360:ID8C%=0'ALL TEMP > 8 DEG C.
1180 SIB%=1:SIR%=720:X%=1:SWM%=MSW%:GOSUB 2160 'OR WHO4F5 FOR NCPM(2)
1190 ICON%=MAX%
1200 IF ICON%>360 THEN ICON%=360
1210 NCPM%(1)=ICON%
1220 IF SN% GOTO 1240
1230 NCPM%(2)=NCPM%(1)
1240 SN%=0'  :WIDTH 80:LOCATE 1,1:PRINT"  ALMOST DONE NOW" 'XERIC CRITERIA
1250 MSW%=-1:IF H$="N" THEN IB%=181:IC%=1 ELSE IF H$="S" THEN IB%=1:IC%=181
1260 SIB%=IB%:SIR%=120:X%=1:SWM%=MSW%:GOSUB 2160'FOR NCCD
1270 NCCD%=MAX%:SIB%=IC%
1280 SIR%=120:X%=3:SWM%=MSW%
1290 GOSUB 2160:NCCM%=MAX%
1300 IF ND%(3)=360 GOTO 1390'SPECIAL CASES
1310 IF ND%(1)=360 GOTO 1420'SPECIAL CASES
1320 IF ND%(1)=0 GOTO 1360'SPECIAL CASES
1330 TU%=0
1340 SIB%=IB%:SIR%=180:X%=1:SWM%=TU%:GOSUB 2160:NCSM%=MAX%
1350 SIB%=IC%:GOSUB 2160:NCWM%=MAX%:GOTO 1400
1360 TU%=-1
1370 SIB%=IB%:X%=3:SWM%=TU%:GOSUB 2160:NCSP%=MAX%
1380 SIB%=IC%:SIR%=180:SWM%=TU%:GOSUB 2160:NCWP%=MAX%:GOTO 1400
1390 NCSM%=180:NCWM%=180:NCSP%=180:NCWP%=180:NTSU%(3)=180:NTWI%(3)=180:GOTO 1420
1400 JB%=IB%:JR%=180:GOSUB 2380:FOR I=1 TO 3:NTSU%(I)=NZD%(I):NEXT I
1410 FOR I=1 TO 3:NTWI%(I)=ND%(I)-NTSU%(I):NEXT I
1420 IF TC% OR TU% THEN GOSUB 2990'CALCULATE CALENDER,WH04T4
1430 GOSUB 3390:CC$=CHR$(7)+CHR$(0)+CHR$(7)+CHR$(0)+CHR$(7)'CLASSIFY NEW
1440 '                      CLASSIFY CUF8  SUBROUTINE REMOVED
1460 CLOSE #1:C=0:FOR I = 1 TO LEN(FD$):IF MID$(FD$,I,1)="." THEN C= I
1461 NEXT I
1462 HOLD$=RIGHT$(FD$,3)
1463 IF (LEFT$(HOLD$,1)="F" OR LEFT$(HOLD$,1)="f") AND (MID$(HOLD$,2,1)="L" OR       MID$(HOLD$,2,1)="l") AND (RIGHT$(HOLD$,1)="x" OR RIGHT$(HOLD$,1)="X") THEN      FDF$=FD$:GOTO 1580'        don't write to file
1466 'IF C=0 THEN GOTO 1468
1467 FDF$=LEFT$(FD$,C)+"FLX":GOTO 1469
1468 'FDF$=FDF$+".FLX"
1469 OPEN FDF$ FOR OUTPUT AS #1:CLOSE #1:KILL FDF$'get rid of file for name          to work without error
1470 NAME FD$ AS FDF$
1471 OPEN FDF$ FOR APPEND AS #2
1480 WRITE #2,ANS$,ID5C%,LT5C%,ID8C%,LT8C%,NCCD%,NCCM%,NCSM%,NCWM%,DIV$,Q$
1490 WRITE #2,NCSP%,NCWP%,TC%,TU%,SWT%,SWP%
1500 FOR I=1 TO 360:PRINT #2,IDAY%(I);",";:NEXT I
1505 WRITE #2,WHC%
1510 FOR I=1 TO 6:WRITE #2,NBD%(I),NED%(I),NBD8%(I),NED8%(I):NEXT I
1520 FOR I=1 TO 3:WRITE #2,ND%(I),NZD%(I),NSD%(I),NTSU%(I),NTWI%(I):NEXT I
1530 WRITE #2,NCPM%(1),NCPM%(2)
1540 FOR I=1 TO 5:WRITE #2,CD%(I):NEXT I
1560 IF NOT TC% AND NOT TU% GOTO 1580
1570 FOR I=1 TO 360:PRINT #2,NTD$(I);",";:NEXT I
1580 CLOSE #2:PRINT CHR$(7):GOSUB 1600:CLOSE #1,#2
1590 'END
1600 LOCATE 25,1
1601 INPUT"Proceed to next step";QA$:IF LEFT$(QA$,1)="N" OR LEFT$(QA$,1)="n"         THEN CLOSE #2:RUN"NSM
1610 USED=USED+1:CLOSE #2:CHAIN "STORVAR6"
1620 RETURN

1630 FOR I=1 TO 3:DMC%(I)=0:CC%(I)=0:NEXT I:ZSW%=0:DPMC%=0:PMC%=K%'WH04F1
1640 LP=MP(IM)/2!
1650 NPE=(LP-MPE(IM))/2!
1660 IF NPE<0! THEN NPE=-NPE:CNPE=NPE ELSE IF NPE=0! THEN GOTO 1920 ELSE ZSW%=-1     :CNPE=NPE
1670 FOR I3=1 TO 64:IF NOT ZSW% GOTO 1840
1680 IF NPE <= 0! GOTO 1920
1690 IF SL(I3)>=FSL GOTO 1910
1700 ESL=FSL-SL(I3)
1710 IF ESL >= NPE THEN GOTO 1740 ELSE SL(I3)=FSL
1720 NPE=NPE-ESL
1730 GOTO 1750
1740 SL(I3)=SL(I3)+NPE:NPE=0!
1750 GOSUB 2880:IF ZSW% THEN GOSUB 3900 ELSE GOSUB 3990
1760 KK%=K%:K%=PMC%
1770 IF KK%=PMC% GOTO 1910
1780 IF NPE <= 0! GOTO 1920
1790 RPE=CNPE-NPE
1800 DMC%(K%)=INT(15*RPE/CNPE)-DPMC%
1810 IGMC%=DMC%(K%):DPMC%=DMC%(K%)+DPMC%:DMC%(K%)=0
1820 GOSUB 1960
1830 PMC%=KK%:K%=KK%:GOTO 1910
1840 IF NPE<=0! GOTO 1920
1850 NR%=DP%(I3)
1860 IF SL(NR%)<=0! GOTO 1910
1870 RPD=SL(NR%)*DR(I3):IF NPE<=RPD GOTO 1890
1880 SL(NR%)=0!:NPE=NPE-RPD:GOTO 1750
1890 SL(NR%)=SL(NR%)-(NPE/DR(I3))
1900 NPE=0!:GOTO 1750
1910 NEXT I3
1920 DMC%(K%)=15-DPMC%:
1930 IGMC%=DMC%(K%):DMC%(K%)=0
1940 GOSUB 1960
1950 RETURN

1960 IE%=IE%+IGMC%:FOR I%=IB% TO IE%:IDAY%(I%)=K%' WH04F4
1970 VIEW (149,40)-(318,184):WINDOW SCREEN (1,0)-(30,12)
1980 IF I%>30 THEN II=(I% MOD 30)-1:ELSE II=I%
1990 MM=INT (I%/30):IF I% MOD 30 =0 THEN MM=MM-1
2000 IF II=-1 THEN II=29
2010 IF MM<0 THEN MM=0
2020 LINE (II,MM)-(II+1,MM+1),K%-1,BF
2030 GOSUB 4460
2040 VIEW (24,30)-(88,94),,3:WINDOW (0,0)-(8,8):NEXT I%
2050 IB%=IE%+1:ND%(K%)=ND%(K%)+IGMC%
2060 RETURN
2070 HP=(MP(IM)/2!):FOR I3=1 TO 64
2080 IF SL(I3)>=FSL GOTO 2140
2090 ESL=FSL-SL(I3):IF ESL>=HP GOTO 2120
2100 SL(I3)=FSL:HP=HP-ESL:IF GOGR% THEN GOSUB 3900
2110 GOTO 2140
2120 SL(I3)=SL(I3)+HP:IF GOGR% THEN GOSUB 3900
2130 GOTO 2150
2140 NEXT I3
2150 RETURN
2160 NS%(X%)=0:MAX%=0:IFIN%=0:SW%=0:SIZ%=SIB%+SIR%-1'WH04F5
2170 FOR N=SIB% TO SIZ%:N1%=N+1
2180 IF N1%>360 THEN N1%=N1%-360
2190 IF N>360 THEN SI%=N-360 ELSE SI%=N
2200 IF SWM% GOTO 2250
2210 IF IDAY%(SI%)<>X% GOTO 2230
2220 GOTO 2350
2230 IF IDAY%(N1%)=X% GOTO 2300
2240 GOTO 2280
2250 IF IDAY%(SI%)=X% GOTO 2270
2260 GOTO 2350
2270 IF IDAY%(SI%)<>IDAY%(N1%) GOTO 2300
2280 NS%(X%)=NS%(X%)+1:SW%=-1
2290 GOTO 2350
2300 IF SW% GOTO 2320
2310 GOTO 2350
2320 NS%(X%)=NS%(X%)+1:IF NS%(X%)>MAX% GOTO 2340
2330 NS%(X%)=0:SW%=0:GOTO 2350
2340 MAX%=NS%(X%):GOTO 2330
2350 NEXT N:IF SW% THEN IFIN%=NS%(X%)
2360 IF IFIN%>MAX% THEN MAX%=IFIN%
2370 RETURN

2380 FOR IJ=1 TO 3:NZD%(IJ)=0:NEXT IJ:JE%=JB%+JR%-1:FOR L=JB% TO JE%:J%=L'F7
2390 IF J%>360 THEN J%=J%-360
2400 IK%= IDAY%(J%):NZD%(IK%)=NZD%(IK%)+1:NEXT L:RETURN

2410 FOR I=1 TO 12:NJ%(I)=0:NEXT I'WH04T1
2420 ZWT%=0:KJ%=1:IA%=30:IZ%=30
2430 IF CRR!=8! GOTO 2450
2440 IA%=36:IZ%=25
2450 FOR I=1 TO 12
2460 FOR J=1 TO 14:C%(J)=0:NEXT J
2470 M0%=I-1:M1%=I:M2%=I+1:IF M2%>12 THEN M2%=M2%-12
2480 IF M0%<1 THEN M0%=M0%+12
2490 C%(1)=T(M1%)<CRR!:C%(2)=T(M2%)>CRR!
2500 C%(3)=T(M0%)<CRR!:C%(4)=T(M1%)=CRR!
2510 C%(5)=T(M2%)>CRR!:C%(6)=T(M2%)<CRR!
2520 C%(7)=T(M1%)>CRR!:C%(8)=T(M0%)>CRR!
2530 C%(9)=C%(1) AND C%(2):C%(10)=C%(3) AND C%(4) AND C%(5)
2540 C%(11)=C%(9) OR C%(10)
2550 C%(12)=C%(6) AND C%(7):C%(13)=C%(8) AND C%(6) AND C%(4)
2560 C%(14)=C%(12) OR C%(13)
2570 IF C%(11) GOTO 2590
2580 IF C%(14) GOTO 2620 ELSE GOTO 2650
2590 NJ%(KJ%)=M0%*30!+IA%+INT(30!*(CRR!-T(M1%))/(T(M2%)-T(M1%)))
2600 IF NJ%(KJ%)>360 THEN NJ%(KJ%)=NJ%(KJ%)-360
2610 KJ%=KJ%+1:ZWT%=-1:GOTO 2650
2620 NJ%(KJ%)=M0%*30!+IZ%+INT(30!*(T(M1%)-CRR!)/(T(M1%)-T(M2%)))
2630 IF NJ%(KJ%)>360 THEN NJ%(KJ%)=NJ%(KJ%)-360
2640 KJ%=KJ%+1:ZWT%=0
2650 NEXT I
2660 IF NOT ZWT% GOTO 2700
2670 LE%=KJ%-2:NPRO%=NJ%(1)
2680 FOR I=1 TO LE%:NJ%(I)=NJ%(I+1):NEXT I
2690 NJ%(LE%+1)=NPRO%
2700 NPJ%=(KJ%-1)/2:FOR I=1 TO NPJ%
2710 IB%=2*I-1:IE%=2*I:NBJ%(I)=NJ%(IB%)
2720 NEJ%(I)=NJ%(IE%)
2730 NEXT I
2740 RETURN

2750 ZSW%=0:LP=MP(IM)/2!:NPE=(LP-MPE(IM))/2!:IF NPE <= 0! GOTO 2760                 ELSE ZSW%=-1:GOTO 2770'WH04B1
2760 NPE=0!-NPE
2770 FOR I3=1 TO 64:IF NOT ZSW% GOTO 2820
2780 IF SL(I3) >= FSL GOTO 2860
2790 ESL=FSL-SL(I3):IF ESL>=NPE GOTO 2810
2800 SL(I3)=FSL:NPE=NPE-ESL:GOTO 2860
2810 SL(I3)=SL(I3)+NPE:GOTO 2870
2820 NR%=DP%(I3):IF SL(NR%)<=0! GOTO 2860
2830 RPE=SL(NR%) * DR(I3):IF NPE <= RPE GOTO 2850
2840 SL(NR%)=0!:NPE=NPE-RPE:GOTO 2860
2850 SL(NR%)=SL(NR%)-(NPE/DR(I3)):NPE=0!:GOTO 2870
2860 NEXT I3
2870 RETURN

2880 FOR I%=1 TO 3:CC%(I%)=0:NEXT I%:FOR I%=1 TO 6:PC%(I%)=0:NEXT I%'WH04F3
2890 PC%(1)=SL(9)<=0!:PC%(2)=SL(17)<=0!:PC%(3)=SL(25)<=0!:CC%(1)=PC%(1) AND          PC%(2) AND PC%(3)
2900 CC%(2)=NOT CC%(1) AND (PC%(1) OR PC%(2) OR PC%(3))
2910 PC%(4)=SL(9)>0!:PC%(5)=SL(17)>0!
2920 PC%(6)=SL(25)>0!:CC%(3)=PC%(4) AND PC%(5) AND PC%(6)
2930 FOR I%=1 TO 3
2940 IF NOT CC%(I%) GOTO 2970
2950 K%=I%
2960 GOTO 2980
2970 NEXT I%
2980 RETURN

2990 KL$(0)="$":KL$(1)="-":KL$(2)="5":KL$(3) ="8":FOR I=1 TO 24:KR%(I)=0:NEXT I      'WH04T4
3000 IF NBD%(1)<0 AND NBD8%(1)<0 GOTO 3350
3010 IF NBD%(1)=0 AND NBD8%(1)<0 GOTO 3370
3020 IF NBD8%(1)<0 THEN NBD8%(1)=0
3030 FOR I=1 TO 6:KR%(I)=NBD%(I):M%(I)=2:NEXT I
3040 FOR I=7 TO 12:KT%=I-6:IF NED%(KT%)=0 GOTO 3070
3050 KR%(I)=NED%(KT%)+1:IF KR%(I)>360 THEN KR%(I)=1
3060 M%(I)=1
3070 NEXT I
3080 FOR I=13 TO 18:KT%=I-12:KR%(I)=NBD8%(KT%):M%(I)=3:NEXT I
3090 FOR I=19 TO 24:KT%=I-18:IF NED8%(KT%)=0 GOTO 3130
3100 KR%(I)=NED8%(KT%)+1
3110 IF KR%(I)>360 THEN KR%(I)=1
3120 M%(I)=2
3130 NEXT I
3140 NT%=23: FOR I=1 TO 23:STT%=0:FOR J=1 TO NT%
3150 IF KR%(J) <= KR%(J+1) GOTO 3180
3160 ITEMP%=KR%(J):ITM%=M%(J):KR%(J)=KR%(J+1)
3170 M%(J)=M%(J+1):KR%(J+1)=ITEMP%:M%(J+1)=ITM%:STT%=-1
3180 NEXT J:IF NOT STT% GOTO 3210
3190 NT%=NT%-1
3200 NEXT I
3210 IMA%=M%(24):NUL%=0
3220 FOR I=1 TO 24:IF KR%(I)=0 THEN NUL%=NUL%+1:NEXT I
3230 FOR I=1 TO 24:KK%=I:IPL%=I+NUL%:IF IPL%>24 GOTO 3250
3240 KR%(I)=KR%(IPL%):M%(I)=M%(IPL%):NEXT I
3250 FOR I=KK% TO 24:KR%(I)=0:NEXT I
3260 IF KR%(1)=1 GOTO 3290
3270 IE%=KR%(1)-1
3280 FOR I=1 TO IE%:NTD$(I)=KL$(IMA%):NEXT I
3290 NN%=24-NUL%: FOR I=1 TO NN%
3300 NS%=M%(I):IB%=KR%(I)
3310 IE%=KR%(I+1)-1:IF KR%(I+1)=0 THEN IE%=360
3320 FOR J=IB% TO IE%:NTD$(J)=KL$(NS%)
3330 NEXT J:NEXT I
3340 GOTO 3380
3350 FOR I=1 TO 360:NTD$(I)=KL$(1):NEXT I
3360 GOTO 3380
3370 FOR I=1 TO 360:NTD$(I)=KL$(2):NEXT I
3380 RETURN
3390 DIV$=" ":Q$=" ":IF SWT% THEN ANS$ ="Perudic":GOTO 3570
3400 IF NSD%(1)>(LT5C%/2) AND NCPM%(2)<90 THEN ANS$="Aridic":GOTO 3450
3410 IF TMA<22 AND DIF >= 5 AND NCCD% >= 45 AND NCCM% >= 45 THEN ANS$="Xeric":       GOTO 3470
3420 IF ND%(1)+ND%(2)<90 THEN ANS$="Udic":GOTO 3480
3430 IF (TRR$<>"pergelic") AND (TRR$<>"cryic") THEN ANS$="Ustic":GOTO 3510
3440 ANS$="Undefined":DIV$=ANS$:GOTO 3580
3450 IF ND%(1)=360 THEN Q$="Extreme":GOTO 3560
3460 IF NCPM%(2) <= 45 THEN Q$="Typic":GOTO 3560 ELSE Q$="Weak":GOTO 3560
3470 IF NCCD%>90 THEN Q$="Dry":GOTO 3560 ELSE Q$="Typic":GOTO 3560
3480 IF ND%(1)+ND%(2)<30 THEN Q$="Typic":GOTO 3560
3490 IF DIF<5 THEN Q$ ="Dry":DIV$="Tropudic":GOTO 3580
3500 DIV$="Tempudic":Q$="Dry":GOTO 3580
3510 IF DIF >= 5 THEN DIV$="Tempustic" ELSE GOTO 3540
3520 IF NCCM% <= 45 THEN Q$="Typic" ELSE IF NCCD%>45 THEN Q$="Xeric"                 ELSE Q$="Wet"
3530 GOTO 3580
3540 IF NCPM%(2)<180 THEN Q$="Aridic" ELSE IF NCPM%(2)<270 THEN Q$="Typic"           ELSE Q$="Udic"
3550 DIV$="Tropustic":GOTO 3580
3560 DIV$=ANS$:GOTO 3580
3570 NCSM%=180:NCWM%=180:NCSP%=180:NCWP%=180:NTSU%(3)=180:NTWI%(3)=180
3580 RETURN
3590 REM CUF8
3600 SCREEN 1:COLOR 12,1:KEY OFF:CLS
3610 LOCATE 25,11:PRINT"SOIL MOISTURE PROFILE"
3620 VIEW(104,40)-(232,168),,3
3630 WINDOW(0,0)-(8,8)
3640 L$="12345678":FOR I=1 TO LEN(L$):LOCATE 5+I*2,10
3650 PRINT MID$(L$,I,1):NEXT I
3660 FOR I=7 TO 1 STEP -1
3670 LINE(0,I)-(8,I),3:NEXT I
3680 FOR I=1 TO 7:LINE(I,8)-(I,0),3:NEXT I
3690 FOR I=7 TO 0 STEP -1
3700 FOR J=0 TO 7:PAINT(J+.1,I+.9),2,3:NEXT J:NEXT I
3710 RETURN
3720 FOR I=1 TO 64:ROW%=INT((DP%(I)-1)/8):KOL%=DP%(I)-INT(ROW%*8)
3730 ROW=7.9-ROW%:KOL=KOL%-.1
3740 PAINT(KOL,ROW),0,3:NEXT I
3750 RETURN
3760 SCREEN 1:VIEW:WINDOW:CLS:COLOR 12,1:SND=7000
3770 GOSUB 4330
3780 LOCATE 2,2:PRINT"INITIAL SOIL CONDITIONS"
3790 VIEW(24,30)-(88,94),,3
3800 WINDOW(0,0)-(8,8)
3810 FOR I=7 TO 1 STEP -1
3820 LINE(0,I)-(8,I),3:NEXT I
3830 FOR I=1 TO 7:LINE(I,8)-(I,0),3:NEXT I
3840 FOR I=1 TO 64:IF SL(I)=FSL THEN KLR%=2
3850               IF SL(I)=FSL GOTO 3870
3860               IF SL(I)=0! THEN KLR%=0 ELSE KLR%=1
3870 ROW%=INT((I-1)/8):KOL%=I-INT(ROW%*8):ROW=7.9-ROW%:KOL=KOL%-.1
3880 PAINT(KOL,ROW),KLR%,3:NEXT I
3890 LOCATE 2,2:PRINT"PROCESSING SOIL WATER MOVEMENTS":OUT &H3D8, &H2A :             RETURN

3900 LOCATE 16,4:PRINT "ADDITION ":VIEW:WINDOW:PUT (93,42),P,PSET:VIEW (24,30)-      (88,94),,3:WINDOW(0,0)-(8,8)
3910 ROW%=INT((I3-1)/8):KOL%=I3-INT(ROW%*8)
3920 ROW=7.9-ROW%:KOL=KOL%-.1
3930      IF SL(I3)=FSL THEN KLR%=2:GOTO 3950
3940      IF SL(I3)=0! THEN KLR%=0 ELSE KLR%=1
3950 PAINT(KOL,ROW),KLR%,3:IF KOL =.9 AND (ROW =4.9 OR ROW=5.9 OR ROW =6.9)          THEN BEEP
3960 SND=SND+100:IF SENABLE =1 THEN SOUND SND,2
3970 FOR DELAY= 1 TO DLAY:NEXT DELAY
3980 RETURN

3990 LOCATE 16,4:PRINT "DEPLETION":VIEW:WINDOW:PUT (93,42),MT,PSET:VIEW (24,30)      -(88,94),,3:WINDOW(0,0)-(8,8)
4000 ROW%=INT((NR%-1)/8):KOL%=NR%-INT(ROW%*8)
4010 ROW=7.9-ROW%:KOL=KOL%-.1
4020      IF SL(NR%)=FSL THEN KLR%=2:GOTO 4040
4030      IF SL(NR%)=0! THEN KLR%=0 ELSE KLR%=1
4040 PAINT(KOL,ROW),KLR%,3:IF KOL =.9 AND (ROW =4.9 OR ROW = 5.9 OR ROW = 6.9)       THEN BEEP
4050 SND=SND-100:IF SND<100 THEN SND=SND + 100
4051 IF SENABLE =1 THEN SOUND SND,2
4060 FOR DELAY = 1 TO DLAY:NEXT DELAY
4070 RETURN

4080 FOR I=1 TO 40:LOCATE 2,I:PRINT" ";:NEXT I:LOCATE 2,9:PRINT "                             ";:FOR DELAY = 1 TO DLAY:NEXT DELAY:LOCATE 2,9:                        PRINT"MOISTURE CONTROL SECTION": LOCATE 4,6:PRINT"TWO LAYERS  "
4081 LOCATE 25,1:PRINT"Press F1 to pause       ";
4090 FOR I=6 TO 5 STEP -1:FOR J=0 TO 7:PAINT(J+.1,I+.9),1 ,3:NEXT J:NEXT I:          GOTO 480
4100 FOR I=1 TO 40:LOCATE 2,I:PRINT" ";:NEXT I:LOCATE 4,6:                           PRINT"  WATER ENTERS LAYER PER LAYER    "
4110 FOR DELAY = 1 TO 5*DLAY:NEXT DELAY
4120 FOR I=7 TO 0 STEP -1
4130 FOR J=0 TO 7:PAINT(J+.1,I+.9),2,3
4140 SND=SND+100:IF SENABLE =1 THEN SOUND SND,2:SOUND 30000,2
4150 NEXT J:FOR K=1 TO 300:NEXT K:NEXT I
4160 GOTO 510
4170 FOR I=1 TO 40:LOCATE 2,I:PRINT" ";:NEXT I:LOCATE 4,6:PRINT "                                  ";:FOR DELAY = 1 TO DLAY:NEXT DELAY:LOCATE 4,8:                   PRINT"    WATER DEPLETES IN SLANTS  "
4180 FOR I=1 TO 64:ROW%=INT((DP%(I)-1)/8):KOL%=DP%(I)-INT(ROW%*8)
4190 ROW=7.9-ROW%:KOL=KOL%-.1
4200 PAINT(KOL,ROW),0,3
4210 SND=SND-100:IF SENABLE = 1 THEN SOUND SND,2:SOUND 30000,2
4220 NEXT I:FOR DELAY = 1 TO 25*DLAY:NEXT DELAY:RETURN
4230 '
4240 ' routine to pause program on F1
4250 '
4260 LOCATE 25,1:PRINT SPACE$(39);:LOCATE 25,1:PRINT "PAUSING  (hit a key)";
4270 AA$=INKEY$:IF AA$="" THEN 4270:ELSE LOCATE 25,1:                                PRINT "Press F1 to pause       ";:RETURN
4280 '
4290 ' toggle sound on F2
4300 '
4310 IF SENABLE =1 THEN SENABLE =0:ELSE SENABLE =1
4320 RETURN
4330 '
4340 ' ROUTINE TO DRAW CALENDAR
4350 '
4360 OUT &H3D8,&H22 ' turn video display off
4370 DIM MON$(12):MON$(1)="Jan.":MON$(2)="Feb.":MON$(3)="Mar.":MON$(4)="Apr.":       MON$(5)="May ":MON$(6)="June":MON$(7)="July":MON$(8)="Aug.":MON$(9)="Sept"      :MON$(10)="Oct.":MON$(11)="Nov.":MON$(12)="Dec."
4380 LINE(25,143)-(37,151),3,B:LOCATE 19,6:PRINT "Dry"
4381 LINE(25,151)-(37,159),1,BF:LOCATE 20,6:PRINT "M/D"
4382 LINE(25,159)-(37,167),2,BF:LOCATE 21,6:PRINT "Moist"
4389 Y=30:LOCATE 1,1
4390 FOR III= 1 TO 12
4400 PRINT MON$(III);:GET (0,0)-(31,7),MON:Y=Y+12:PUT (116,Y),MON,PSET:              LOCATE 1,1:PRINT "     ";:LOCATE 1,1:NEXT III
4410 LOCATE 4,17:PRINT "Days    10     20     30";
4420 VIEW (149,40)-(318,184),0,3:WINDOW (1,0)-(30,12):FOR I= 0 TO 20 STEP 10:        LINE (I,1)-(I,30):NEXT I:FOR I= 1 TO 13:LINE (1,I)-(30,I),3:NEXT I
4430 '
4440 ' sub-subroutine to redraw lines only
4450 '
4460 FOR I= 0 TO 20 STEP 10:LINE (I,0)-(I,12):NEXT I:FOR I= 0 TO 12:LINE (1,I)-      (31,I),3:NEXT I
4470 RETURN
7000 '           error checking
7005 '
7010 V=ERR
7015 IF V=53 THEN BEEP:PRINT:PRINT:PRINT"Can't find ";FD$;:                          INPUT" Re-enter filename ",FD$:DISK$=FD$:RESUME
7020 IF V= 62 THEN BEEP:PRINT:PRINT:PRINT FD$;" is not the right kind of file.":     PRINT"This program uses files that have a suffix of .INT or .PRT": CLOSE #1     :RESUME 50
7040 ON ERROR GOTO 0    ' for any unaccounted-for errors
7999 '
8000 '          directory of disk
8010 CLS
8020 INPUT "Which disk? (Press ENTER for default) ",DISK$
8030 IF DISK$="" THEN FILES:GOTO 8070
8040 IF LEFT$(DISK$,1)="C" OR LEFT$(DISK$,1)="c" THEN DISK$=DISK$+"*.*":             CHDIR"c:\":GOTO 8060
8050 DISK$=LEFT$(DISK$,1)+":*.*
8060 FILES DISK$
8070 PRINT "Hit a key to continue"
8080 F$=INKEY$:IF F$="" THEN 8080
8090 RETURN 50
9000 '
9010 '          return to main menu
9020 CLOSE #1, #2:RUN"NSM
15000 ' bail out on f10
15010 SYSTEM
