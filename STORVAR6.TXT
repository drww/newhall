1  CLS:ON ERROR GOTO 5000:WIDTH 80
2 KEY(10) ON:ON KEY(10) GOSUB 10000:KEY(9) ON:ON KEY(9) GOSUB 5400
5 PRINT:PRINT"NSM - Cornell University":PRINT"Printouts":PRINT:PRINT
40 COMMON USED,FDF$:OPTION BASE 1:DIM IDAY%(360),MP(12),MPE(12),T(12),MO(12),       NTD$(361),MTH$(12),ARRAY(3):KEY OFF
41 PRINT:IF USED >0 THEN GOTO 50 ELSE INPUT "Name of file to be printed";FDF$:     DD$="  ":IF FDF$="" THEN GOTO 41
42 IF FDF$="?" THEN GOSUB 5200
45 CLS
50 OPEN "I",#1,FDF$
60 INPUT #1,ST$,CO$,RLA1,RLA2,H$,RLO1,RLO2,EW$,EL,ARF,AEV,TRR$,TMA,ST,WT,DIF,X,X
65 FOR I = 1 TO 12:INPUT #1,MP(I),T(I),MPE(I):NEXT I
70 INPUT #1,ANS$,ID5C%,LT5C%,ID8C%,LT8C%,NCCD%,NCCM%,NCSM%,NCWM%,DIV$,Q$
80 INPUT #1,NCSP%,NCWP%,TC%,TU%,SWT%,SWP%
90 FOR I = 1 TO 360:INPUT #1,IDAY%(I):NEXT I
95 INPUT #1,WHC%
100 FOR I=1 TO 6:INPUT #1,NBD%(I),NED%(I),NBD8%(I),NED8%(I):NEXT I
110 FOR I = 1 TO 3:INPUT #1,ND%(I),NZD%(I),NSD%(I),NTSU%(I),NTWI%(I):NEXT I
120 INPUT #1,NCPM%(1),NCPM%(2)
130 FOR I = 1 TO 5:INPUT #1,CD%(I):NEXT I
141 INPUT"Do you want to print this on the printer (Y/N)";AS$:CLS
142 IF AS$<>"Y" AND AS$<>"y" AND AS$<>"N" AND AS$<>"n" THEN GOTO 141
143 IF ASC(AS$)>97 THEN AS$=CHR$(ASC(AS$)-32)
145 IF AS$="Y" THEN LPRINT " ";    ' force a printer error if it's off
150 IF NOT TC% AND NOT TU% GOTO 155 ELSE FOR I = 1 TO 360:INPUT #1,NTD$(I):NEXT I
155 IF AS$="Y" THEN GOTO 6000
160 PRINT: PRINT "Station:";ST$;:PRINT "     Country:";CO$;:LOCATE 2,47:PRINT "Latit:  ";:PRINT USING "###  ";RLA1;:PRINT USING "##  ";RLA2;:PRINT "  ";H$
170 PRINT"Elevation: ";EL;:LOCATE 3,47:PRINT "Longit: ";: PRINT USING "###  ";      RLO1;:PRINT USING "##  ";RLO2;           :PRINT "  ";EW$:PRINT
171 PRINT"Annual rainfall";:PRINT USING "#####";ARF;:PRINT" mm";
172 PRINT"                Waterholding capacity: ";WHC%;" mm"
180 MTH$(1)="JAN":MTH$(2)="FEB":MTH$(3)="MAR":MTH$(4)="APR":MTH$(5)="MAY":MTH$(6)="JUN"
190 MTH$(7)="JUL":MTH$(8)="AUG":MTH$(9)="SEP":MTH$(10)="OCT":MTH$(11)="NOV":MTH$(12)="DEC"
200 PRINT"Temperature regime:  ";TRR$,:PRINT"      Moisture regime: ";ANS$
210 PRINT:PRINT:PRINT TAB(12)"SOIL CLIMATIC REGIME ACCORDING TO NEWHALL COMPUTATION"
220 PRINT TAB(12)"(soil temp.=air temp.+2.5 C; amplit. reduced by 1/3)"
230 PRINT:PRINT"     ";:FOR I = 1 TO 12:PRINT MTH$(I);:PRINT"   ";:NEXT I:PRINT
240 PRINT:PRINT TAB(3)"monthly rainfall (mm)":PRINT"   ";:FOR I = 1 TO 12:PRINT USING "###.# ";MP(I);:NEXT I
250 PRINT:PRINT TAB(3)"monthly air temperatures (Celcius)":PRINT"   ";:FOR I = 1 TO 12:PRINT USING"###.# ";T(I);:NEXT I
260 PRINT:PRINT TAB(3)"monthly evapotranspiration (Thornthwaite), mm. ":PRINT"   ";:FOR I = 1 TO 12
270 PRINT USING "###.# ";MPE(I);:NEXT I
275 LOCATE 24,1:INPUT"Press ENTER to continue ",A$:CLS
280 IF TC% GOTO 360    'OR TU% GOTO 360
290 PRINT"                                MOISTURE CALENDAR"
300 PRINT"                    ";:PRINT"      1 = dry ; 2 = m/d ; 3 = moist":PRINT
310 PRINT"                    ";:PRINT"     1************15**************30":PRINT
320 FOR J = 1 TO 12:EM%=J * 30:BM%=J*30-29
330 PRINT"                    ";:PRINT MTH$(J);"  ";:FOR I = BM% TO EM%:PRINT USING "#";IDAY%(I);:NEXT I:PRINT
340 NEXT J:PRINT
350 GOTO 435
360 PRINT"               TEMPERATURE CALENDAR               MOISTURE CALENDAR"
370 PRINT"         (- : T<5) (5 : 5<T<8) (8 : T>8)    1 = DRY ; 2 = M/D ; 3 = MOIST"
380 PRINT:PRINT"         1************15**************30   1************15**************30":PRINT
390 FOR J = 1 TO 12:EM%=J * 30:BM%=J*30-29
400 PRINT TAB(5)MTH$(J);"  ";:FOR I = BM% TO EM%
410 PRINT NTD$(I);:NEXT I:PRINT "    ";
420 FOR I = BM% TO EM%:PRINT USING "#";IDAY%(I);:NEXT I:PRINT
430 NEXT J
435 LOCATE 24,1:INPUT"Press ENTER to continue ",A$:CLS
440 PRINT:FOR I = 1 TO 78:PRINT"_";:NEXT I
450 PRINT:PRINT"|  Number of cumulative days that     |  Highest number of consecutive days  |"
460 PRINT"|    the moisture control section     |           that the MCS is            |"
470 FOR I = 1 TO 78:PRINT"-";:NEXT I
480 PRINT"| During one year | When soil temp is |  Moist in  |     Dry    |    Moist   |"
490 PRINT"|       is        |  above 5 deg. C   | some parts |after summer|after winter|"
500 PRINT"| DRY  M/D  MOIST |  DRY  M/D  MOIST  | YEAR   T>8 |  solstice  |  solstice  |"
510 FOR I = 1 TO 77:PRINT"-";:NEXT I:PRINT "-"
520 PRINT "| ";:FOR I=1 TO 3:PRINT USING "###";ND%(I);:PRINT"  ";:NEXT I:PRINT" |  ";
530 FOR I=1 TO 3:PRINT USING "###";NSD%(I);:PRINT"  ";:NEXT I:PRINT"  |  ";
540 ' FOR I=1 TO 2:PRINT USING "###";NCPM%(I);:PRINT"   ";:NEXT I:PRINT"|    ";
541 PRINT USING "###";NCPM%(1);:PRINT"   ";:PRINT USING "###";NCPM%(2);:PRINT" |    ";
550 PRINT USING "###";NCCD%;:PRINT"     |    ";:PRINT USING "###";NCCM%;:PRINT"     |"
560 FOR I = 1 TO 77:PRINT"_";:NEXT I:PRINT"_":PRINT"*":                             PRINT"Computed by BASIC program FLEXNSM (FEB 1991)."
570 PRINT"Tentative subdivision: ";:PRINT Q$;" ";DIV$;
571 PRINT" for a waterholding capacity of ";WHC%;" mm"
590 CLOSE #1:LOCATE 24,1:INPUT"Press ENTER to continue ",M$:RUN "STORVAR.TXT"
5000 '               error checking
5005 '
5006 V=ERR
5007 IF V<>25 AND V<>27 THEN GOTO 5050  '  printer off or out of paper
5010 PRINT"Printer is not on or is out of paper.
5015 PRINT"Correct the problem, then type C to continue or R to restart this program"
5020 INPUT M$:IF NOT(M$<>"C" OR M$<>"c" OR M$<>"r" OR M$<>"R") THEN                  PRINT"Type an R or a C only":GOTO 5020
5025 IF M$="R" OR M$="r" THEN V=0:RUN ELSE V=0:RESUME
5050 IF V=53 THEN PRINT" Can't find ";FDF$:RESUME 41
5052 IF V<>24 THEN GOTO 5060
5053 PRINT" Printer problems.  Try turning printer OFF, then ON to clear the problem.":INPUT "Press ENTER when ready. ",A$:RESUME
5060 '       file format problems
5061 IF V<>62 THEN GOTO 5100
5062 CLOSE #1
5065 PRINT FDF$;" is the wrong type of file or wrong format for this program.
5066 IF RIGHT$(FDF$,3)<>"FLX" THEN PRINT" Print files have a suffix of .FLX":PRINT"Try typing the file name with a .FLX suffix this time ":RESUME 41
5080 RESUME 45
5100 ON ERROR GOTO 0    'takes care of any other errors
5200 '
5210 '          directory of a disk
5220 CLS
5230 INPUT"Which disk? (Hit ENTER for default) ",DISK$
5240 IF DISK$="" THEN FILES:GOTO 5280
5250 IF LEFT$(DISK$,1)="c" OR LEFT$(DISK$,1)="C" THEN DISK$=DISK$+"*.*":             CHDIR"c:\":GOTO 5270
5260 DISK$=LEFT$(DISK$,1)+":*.*"
5270 FILES DISK$
5280 PRINT"Hit a key to continue"
5290 F$=INKEY$:IF F$="" THEN 5290
5300 RETURN 41
5400 '
5410 '  return to main menu
5420 CLOSE #1:RUN"nsm
6000 '
6001 '
6002 '                     printer output
6004 '
6010 LPRINT:LPRINT "Station:";ST$;:LPRINT "     Country:";CO$;:                      LPRINT TAB(47)"Latit: ";:LPRINT USING "###  ";RLA1;:                            LPRINT USING "##  ";RLA2;:LPRINT H$
6015 LPRINT"Elevation: ";EL;:LPRINT TAB(47)"Longit:";:LPRINT USING "###  ";RLO1;     :LPRINT USING "##  ";RLO2;:LPRINT EW$:LPRINT:LPRINT"Annual rainfall";:          LPRINT USING "#####";ARF;:LPRINT" mm";
6016 LPRINT"                Waterholding capacity: ";WHC%;" mm"
6020 MTH$(1)="JAN":MTH$(2)="FEB":MTH$(3)="MAR":MTH$(4)="APR":MTH$(5)="MAY":MTH$(6)="JUN"
6025 MTH$(7)="JUL":MTH$(8)="AUG":MTH$(9)="SEP":MTH$(10)="OCT":MTH$(11)="NOV":MTH$(12)="DEC"
6030 LPRINT"Temperature regime:  ";TRR$,:LPRINT"      Moisture regime: ";ANS$
6035 LPRINT:LPRINT:LPRINT TAB(12)"SOIL CLIMATIC REGIME ACCORDING TO NEWHALL COMPUTATION"
6040 LPRINT TAB(12)"(soil temp.=air temp.+2.5 C; amplit. reduced by 1/3)"
6045 LPRINT:LPRINT"     ";:FOR I = 1 TO 12:LPRINT MTH$(I);:LPRINT"   ";:NEXT I:LPRINT
6050 LPRINT:LPRINT TAB(3)"monthly rainfall (mm)":LPRINT"   ";:FOR I = 1 TO 12:LPRINT USING "###.# ";MP(I);:NEXT I
6055 LPRINT:LPRINT TAB(3)"monthly air temperatures (Celcius)":LPRINT"   ";:FOR I = 1 TO 12:LPRINT USING"###.# ";T(I);:NEXT I
6060 LPRINT:LPRINT TAB(3)"monthly evapotranspiration (Thornthwaite), mm. ":LPRINT"   ";:FOR I = 1 TO 12
6065 LPRINT USING "###.# ";MPE(I);:NEXT I
6070 IF TC% GOTO 6110      'OR TU% GOTO 6110
6075 LPRINT:LPRINT"                                MOISTURE CALENDAR"
6080 LPRINT"                    ";:LPRINT"      1 = dry ; 2 = m/d ; 3 = moist":LPRINT
6085 LPRINT"                    ";:LPRINT"     1************15**************30":LPRINT
6090 FOR J = 1 TO 12:EM%=J * 30:BM%=J*30-29
6095 LPRINT"                    ";:LPRINT MTH$(J);"  ";:FOR I = BM% TO EM%:LPRINT USING "#";IDAY%(I);:NEXT I:LPRINT
6100 NEXT J:LPRINT
6105 GOTO 6150
6110 LPRINT:LPRINT:LPRINT"               TEMPERATURE CALENDAR               MOISTURE CALENDAR"
6115 LPRINT"         (- : T<5) (5 : 5<T<8) (8 : T>8)    1 = DRY ; 2 = M/D ; 3 = MOIST"
6120 LPRINT:LPRINT"         1************15**************30   1************15**************30":LPRINT
6125 FOR J = 1 TO 12:EM%=J * 30:BM%=J*30-29
6130 LPRINT TAB(5)MTH$(J);"  ";:FOR I = BM% TO EM%
6135 LPRINT NTD$(I);:NEXT I:LPRINT "    ";
6140 FOR I = BM% TO EM%:LPRINT USING "#";IDAY%(I);:NEXT I:LPRINT
6145 NEXT J
6150 LPRINT:FOR I = 1 TO 78:LPRINT"_";:NEXT I
6155 LPRINT:LPRINT"|  Number of cumulative days that     |  Highest number of consecutive days  |"
6160 LPRINT"|    the moisture control section     |           that the MCS is            |"
6165 FOR I = 1 TO 78:LPRINT"-";:NEXT I
6170 LPRINT"| During one year | When soil temp is |  Moist in  |     Dry    |    Moist   |"
6175 LPRINT"|       is        |  above 5 deg. C   | some parts |after summer|after winter|"
6180 LPRINT"| DRY  M/D  MOIST |  DRY  M/D  MOIST  | YEAR   T>8 |  solstice  |  solstice  |"
6185 FOR I = 1 TO 77:LPRINT"-";:NEXT I:LPRINT "-"
6190 LPRINT "| ";:FOR I=1 TO 3:LPRINT USING "###";ND%(I);:LPRINT"  ";:NEXT I:LPRINT" |  ";
6195 FOR I=1 TO 3:LPRINT USING "###";NSD%(I);:LPRINT"  ";:NEXT I:LPRINT"  |  ";
6200 ' FOR I=1 TO 2:LPRINT USING "###";NCPM%(I);:LPRINT"   ";:NEXT I:LPRINT"|    ";
6205 LPRINT USING "###";NCPM%(1);:LPRINT"   ";:LPRINT USING "###";NCPM%(2);:LPRINT" |    ";
6210 LPRINT USING "###";NCCD%;:LPRINT"     |    ";:LPRINT USING "###";NCCM%;:LPRINT"     |"
6215 FOR I = 1 TO 77:LPRINT"_";:NEXT I:LPRINT"_":LPRINT"*":                          LPRINT"Computed by BASIC program FLEXNSM (FEB 1991)."
6220 LPRINT"Tentative subdivision: ";:LPRINT Q$;" ";DIV$;
6221 LPRINT" for a waterholding capacity of ";WHC%;" mm"
6225 LPRINT CHR$(27):LPRINT CHR$(12)
6230 CLOSE #1:RUN "STORVAR.TXT"
10000 '      bail out on f10
10010 SYSTEM
